C $Header: /u/gcmpack/MITgcm/pkg/ex3/ex3_recv_rx2.template,v 1.2 2006/02/06 21:09:54 edhill Exp $
C $Name:  $

#include "EX3_OPTIONS.h"

C---+----1----+----2----+----3----+----4----+----5----+----6----+----7-|--+----|
CBOP 0
C     !ROUTINE: EX3_RECV_RX2

C     !INTERFACE:
      SUBROUTINE EX3_RECV_RX2(
     I     bufftag, sendProc,
     I     il,ih,is,  jl,jh,js,  kl,kh,ks,
     I     idl1,idh1, jdl1,jdh1, kdl1,kdh1,
     I     array1,
     I     idl2,idh2, jdl2,jdh2, kdl2,kdh2,
     I     array2,
     B     buff, n_buff,
     I     commType,
     I     myThid )

C     !DESCRIPTION:
C     Scalar exchange routine which simply copies values from an input
C     array to a buffer in the specified order and then, if necessary,
C     calls the appropriate MPI send function.

C     !USES:
      IMPLICIT NONE
#include "SIZE.h"
#include "EEPARAMS.h"
#include "EESUPPORT.h"

C     !INPUT PARAMETERS:
      INTEGER bufftag, sendProc
      INTEGER il,ih,is, jl,jh,js, kl,kh,ks
      INTEGER idl1,idh1, jdl1,jdh1, kdl1,kdh1
      INTEGER idl2,idh2, jdl2,jdh2, kdl2,kdh2
      _RX array1( idl1:idh1, jdl1:jdh1, kdl1:kdh1 )
      _RX array2( idl2:idh2, jdl2:jdh2, kdl2:kdh2 )
      INTEGER n_buff
      _RX buff( n_buff )
      CHARACTER commType
      INTEGER myThid
CEOP

C     !LOCAL VARIABLES
      INTEGER ii,jj,kk, ntb, nbv, nbv2
#ifdef ALLOW_USE_MPI
      INTEGER mpiStatus(MPI_STATUS_SIZE)
      INTEGER mpirc
#endif
      character*(100) msgbuf

C     Calculate buffer sizes
      nbv =  IABS((ih-il+1)/is) 
     &     * IABS((jh-jl+1)/js)
     &     * IABS((kh-kl+1)/ks)
      nbv2 = nbv*2
      IF ( nbv2 .GT. n_buff ) THEN
        WRITE(msgbuf,'(2a)') 'EX3_RECV_RX2 ERROR: buffer too small',
     &       '--please increase EX3_BUFF_FAC in ''EX3_SIZE.h'''
        CALL print_error(msgbuf, mythid)
        WRITE(msgbuf,'(a,i9)') '  current buffer length   = ', n_buff
        CALL print_error(msgbuf, mythid)
        WRITE(msgbuf,'(a,i9)') '  requested buffer length = ', nbv
        CALL print_error(msgbuf, mythid)
        STOP 'ABNORMAL END: S/R EX3_RECV_RX2'
      ENDIF

      IF ( commType .EQ. 'P' ) THEN
C       We may eventually need (?) some sort of synchronization
C       mechanism for multi-threaded mode
      ELSEIF ( commType .EQ. 'M' ) THEN
#ifdef ALLOW_USE_MPI
        CALL MPI_Recv(
     I       buff, nbv2, EX3_MPI_TYPE_RX, sendProc, bufftag,
     I       MPI_COMM_MODEL,
     O       mpiStatus, mpirc )
#endif
      ELSE
        WRITE(msgbuf,'(3a)') 'EX3_RECV_RX2 ERROR: commType ''',
     &       commType, ''' is invalid'
        CALL print_error(msgbuf, mythid)
        STOP 'ABNORMAL END: S/R EX3_RECV_RX2'
      ENDIF

C     Copy the values into the buffer in the specified order
      ntb = 0
      DO kk = kl, kh, ks
        DO jj = jl, jh, js
          DO ii = il, ih, is
            ntb = ntb + 1
            array1( ii, jj, kk ) = buff( ntb )
            array2( ii, jj, kk ) = buff( nbv + ntb )
          ENDDO
        ENDDO
      ENDDO
      
      RETURN
      END

C---+----1----+----2----+----3----+----4----+----5----+----6----+----7-|--+----|

CEH3 ;;; Local Variables: ***
CEH3 ;;; mode:fortran ***
CEH3 ;;; End: ***
